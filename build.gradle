plugins {
    id 'fabric-loom'
    id 'com.gradleup.shadow' version '8.3.5'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

configurations {
    shadowRuntime
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation "org.java-websocket:Java-WebSocket:1.5.4"
    compileOnly "org.java-websocket:Java-WebSocket:1.5.4"
    shadowRuntime "org.java-websocket:Java-WebSocket:1.5.4"
}

tasks.named('shadowJar') {
    archiveClassifier.set('dev-shadow')
    configurations = [project.configurations.shadowRuntime]
    // relocate 'org.java_websocket', 'com.medua.shaded.org.java_websocket'
}

processResources {
    def modVersion = project.version
    def mcVersion = project.minecraft_version
    def loaderVersion = project.loader_version
    def modId = project.mod_id

    inputs.property "version", modVersion
    inputs.property "minecraft_version", mcVersion
    inputs.property "loader_version", loaderVersion
    inputs.property "mod_id", modId

    filteringCharset = "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": modVersion,
                "minecraft_version": mcVersion,
                "loader_version": loaderVersion,
                "mod_id": modId
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

tasks.named('remapJar') {
    dependsOn tasks.named('shadowJar')
    input.set(tasks.named('shadowJar').get().archiveFile)
}

def generatedSrcDir = layout.buildDirectory.dir("generated/sources/buildConfig/java/main")

tasks.register("generateBuildConfig") {
    outputs.dir(generatedSrcDir)

    def modId = project.mod_id
    def modVersion = project.mod_version

    doLast {
        def pkg = "com.medua.apostlesbridgenext.generated.gen"
        def pkgPath = pkg.replace('.', '/')
        def outDir = generatedSrcDir.get().asFile
        def javaFile = new File(outDir, "${pkgPath}/BuildConfig.java")
        javaFile.parentFile.mkdirs()
        javaFile.text = """package ${pkg};

public final class BuildConfig {
    public static final String MODID = \"${modId}\";
    public static final String VERSION = \"${modVersion}\";

    private BuildConfig() {
    }
}
"""
    }
}

sourceSets {
    main {
        java {
            srcDir generatedSrcDir
        }
    }
}

tasks.named("compileJava") {
    dependsOn tasks.named("generateBuildConfig")
}

tasks.named("sourcesJar") {
    dependsOn tasks.named("generateBuildConfig")
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
